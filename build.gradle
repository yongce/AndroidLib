buildscript {
    ext {
        androidProjectCommon = "${rootDir}/android_project_common.gradle"
        androidModuleCommon = "${rootDir}/android_module_common.gradle"
    }
    apply from: "${androidProjectCommon}"

    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.0.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
    }
}

plugins {
    id("com.diffplug.spotless") version "5.16.0"
    id("io.github.gradle-nexus.publish-plugin") version "1.1.0"
    id("org.jetbrains.dokka") version "1.5.30"
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

ext {
    versions.ndkVersion = "21.3.6528147"

    publishEnabled = true
    mavenMeta = [
            'projectUrl': 'https://github.com/yongce/AndroidLib',
            'projectScmConnection': 'https://github.com/yongce/AndroidLib.git',
            'projectScmDevConnection': 'ssh://git@github.com/yongce/AndroidLib.git',
            'projectInceptionYear': '2013',
            'groupId': 'io.github.yongce',
            'version': '2.0.0',
            'developerId': 'yongce',
            'developerName': 'Yongce Tu',
            'developerEmail': 'yongce.tu@gmail.com',
    ]

    // Trick: other projects can redefine the mapping to include the modules directly
    deps.ycdev = [
            'androidBase': project(':baseLib'),
            'androidUi'  : project(':uiLib'),
            'androidJni' : project(':jniLib'),
            'androidTest': project(':testLib'),
    ]
}

spotless {
    kotlin {
        target "**/*.kt"
        ktlint(versions.ktlint).userData(['indent_size' : 'unset'])
    }
}

apply from: "${rootDir}/publish-root.gradle"
apply plugin: 'android-reporting'
