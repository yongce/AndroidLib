apply plugin: 'maven-publish'
apply plugin: 'signing'

group = mavenMeta.groupId
version = mavenMeta.version

if (project.plugins.findPlugin("com.android.library")) {
    android {
        publishing {
            singleVariant("release") {
                withSourcesJar()
                withJavadocJar()
            }
        }
    }
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }

                groupId = mavenMeta.groupId
                artifactId = project.archivesBaseName
                version = mavenMeta.version

                pom {
                    name = project.moduleName
                    description = project.moduleDesc
                    url = mavenMeta.projectUrl

                    scm {
                        url = mavenMeta.projectUrl
                        connection = mavenMeta.projectScmConnection
                        developerConnection = mavenMeta.projectScmDevConnection
                    }

                    licenses {
                        license {
                            name = 'The Apache Software License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id = mavenMeta.developerId
                            name = mavenMeta.developerName
                            email = mavenMeta.developerEmail
                        }
                    }
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(
            rootProject.ext["signing.keyId"],
            rootProject.ext["signing.key"],
            rootProject.ext["signing.password"],
    )
    sign publishing.publications
}